-- programs ------------------------------------------------

entrypoints Program ;

PProgram.   Program ::= [Init] ;

IFnDef.	   Init ::= Type Ident "(" [Arg] ")" Block ;

IVarDef.   Init ::= Type Ident "=" Expr ";" ;

separator nonempty Init "" ;

CopyArg. 	Arg ::= Type Ident;

RefArg.     Arg ::= Type "ref" Ident;

separator  Arg "," ;

-- statements ----------------------------------------------

SBlock.     Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

SEmpty.     Stmt ::= ";" ;

SBStmt.     Stmt ::= Block ;

SDecl.      Stmt ::= Type Ident ";" ;

SInit.      Stmt ::= Init ;

SAss.       Stmt ::= Ident "=" Expr  ";" ;

SIncr.      Stmt ::= Ident "++"  ";" ;

SDecr.      Stmt ::= Ident "--"  ";" ;

SRet.       Stmt ::= "return" Expr ";" ;

SVRet.      Stmt ::= "return" ";" ;

SCond.      Stmt ::= "if" "(" Expr ")" Block  ;

SCondElse.  Stmt ::= "if" "(" Expr ")" Block "else" Block  ;

SWhile.     Stmt ::= "while" "(" Expr ")" Block ;

SExp.      Stmt ::= Expr  ";" ;

-- Types ---------------------------------------------------

TInt.       Type ::= "int" ;

TStr.       Type ::= "string" ;

TBool.      Type ::= "bool" ;

TVoid.      Type ::= "void" ;

TFun.       Type ::= "[" "(" [Arg] ")" "->" Type "]" ;

separator  Type "," ;

-- Expressions ---------------------------------------------

EVar.      Expr6 ::= Ident ;

ELitInt.   Expr6 ::= Integer ;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

EApp.      Expr6 ::= Ident "(" [Expr] ")" ;

EAppLambda. Expr6 ::= "(" Expr ")" "(" [Expr] ")" ;

EString.   Expr6 ::= String ;

ENeg.       Expr5 ::= "-" Expr6 ;

ENot.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

ELambdaExpr.    Expr ::= "lambda" "(" [Arg] ")" "->" Type "." Expr ;

ELambdaExpr.   Expr ::= "λ" "(" [Arg] ")" "->" Type "." Expr ;

ELambdaBlock.   Expr ::= "lambda" "(" [Arg] ")" "->" Type Block ;

ELambdaBlock.   Expr ::= "λ" "(" [Arg] ")" "->" Type Block ;

coercions  Expr 6 ;

separator  Expr "," ;

-- operators -----------------------------------------------

OPlus.      AddOp ::= "+" ;

OMinus.     AddOp ::= "-" ;

OTimes.     MulOp ::= "*" ;

ODiv.       MulOp ::= "/" ;

OMod.       MulOp ::= "%" ;

OLTH.       RelOp ::= "<" ;

OLE.        RelOp ::= "<=" ;

OGTH.       RelOp ::= ">" ;

OGE.        RelOp ::= ">=" ;

OEQU.       RelOp ::= "==" ;

ONE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;

